import fr.avianey.androidsvgdrawable.gradle.SvgDrawableTask

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath('fr.avianey.androidsvgdrawable:gradle-plugin:3.0.0') {
            // should be excluded to avoid conflict
            exclude group: 'xerces'
        }
    }
}

task "svgToPng"(type: SvgDrawableTask) {
    // specify where to pick SVG from
    from = files("design/svg")

    // specify the android res folder
    to = file("app/src/main/res")

    // create qualified directories if missing
    createMissingDirectories = true

    // override files only if necessary
    overwriteMode = 'ifModified'

    // let generate PNG for the following densities only
    targetedDensities = ['mdpi', 'hdpi', 'xhdpi', 'xxhdpi']

    // relative path of the file specifying nine patch specs
    ninePatchConfig = file("design/svg/9patch.json")

    // output format of the generated resources
    outputFormat = 'PNG'
}

task generatePngResources {
    group "Svg"
    description "Generates PNG files from SVG"
    dependsOn tasks.withType(SvgDrawableTask)
}